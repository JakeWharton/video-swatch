import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath libs.kotlin.gradlePlugin
		classpath libs.spotless.gradlePlugin
	}
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
	macosArm64()

	sourceSets {
		configureEach {
			languageSettings.optIn("kotlin.ExperimentalStdlibApi")
			languageSettings.optIn("kotlin.contracts.ExperimentalContracts")
			languageSettings.optIn("kotlinx.cinterop.ExperimentalForeignApi")
		}

		commonMain {
			dependencies {
				implementation libs.clikt
				implementation libs.kotlinx.dateTime
			}
		}
		commonTest {
			dependencies {
				implementation libs.assertk
			}
		}
	}

	targets.withType(KotlinNativeTarget).configureEach {
		binaries.executable {
			entryPoint = "com.jakewharton.videoswatch.main"
		}
		compilations.getByName("main").cinterops {
			create("libffmpeg")
			create("libpng")
		}
	}
}

apply plugin: 'com.diffplug.spotless'
spotless {
	kotlin {
		target("src/**/*.kt")
		ktlint(libs.ktlint.get().version).editorConfigOverride([
			'ktlint_standard_filename': 'disabled',
		])
	}
}
